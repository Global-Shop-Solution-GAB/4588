Program.Sub.Preflight.Start

Program.Sub.Preflight.End

Program.Sub.Main.Start
F.Intrinsic.Control.SetErrorHandler("Main_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sOrderNum.Declare(String)
V.Local.sParams.Declare(String)
V.Local.sSql.Declare(String)
V.Local.iRptID.Declare(Long)
V.Local.sRptID.Declare(String)
V.Local.sValues.Declare(String)
V.Local.iBIRunID.Declare(Long)
V.Local.iLogID.Declare(Long)
V.Local.iRet.Declare(Long)

F.ODBC.Connection!con.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)

F.Intrinsic.Control.SelectCase(V.Caller.Hook)
	'populate hook on sales order screen
	F.Intrinsic.Control.Case(11560)
		V.Passed.000201.Set("Ack W/Out Price")
		
	'script 1 hook on sales order screen
	F.Intrinsic.Control.Case(11910)
		'set the order number
		V.Local.sOrderNum.Set(V.Passed.000003)
		
		F.Intrinsic.Control.If(V.Local.sOrderNum.Length,=,0)
			F.Intrinsic.UI.Msgbox("Select a Sales Order")
			F.Intrinsic.Control.CallSub(exit)
		F.Intrinsic.Control.EndIf
		
		F.Intrinsic.String.LPad(V.Local.sOrderNum,"0",7,V.Local.sOrderNum)
		
		'build data
		F.Intrinsic.String.Build("{0}!*!P!*!!*!|~|CRYSTAL-OVERRIDE|~|",V.Local.sOrderNum,V.Local.sParams)
		F.Global.General.CallWrapperSyncBio(910050,V.Local.sParams)
		
		'get report
		F.Intrinsic.String.Build("Select RptID From V_Bi_Acknwldgmnt Where Order_No = '{0}' and Terminal = '{1}'",V.Local.sOrderNum,V.Caller.Terminal,V.Local.sSql)
		
		F.ODBC.Connection!con.OpenRecordsetRO("rst",V.Local.sSql)

		F.Intrinsic.Control.If(V.ODBC.con!rst.EOF,=,False)
			V.Local.iRptID.Set(V.ODBC.con!rst.FieldVal!Rptid)
		F.Intrinsic.Control.EndIf
		F.ODBC.con!rst.Close
		
		F.Intrinsic.String.LPad(V.Local.iRptID,"0",6,V.Local.sRptid)
		
		'getting report id for custom report that removed price
		F.Global.BI.GetIDFromName("OE_4588_ACKNOWLEDGEMENT.rpt",V.Local.iRptID)
		
		'get runid
		F.Global.BI.GetRunID(V.Local.iBIRunID)
		'start logging
		F.Global.BI.StartLogging(V.Local.iBIRunID,V.Local.iRptID,-1,"",V.Local.iLogID)
		
		'set params for report
		V.Local.sParams.Set("Terminal*!*ReportID*!*IncludesTax*!*Dec*!*Comments")
		F.Intrinsic.String.Build("{0}*!*{1}*!**!*.2*!*",V.Caller.Terminal,V.Local.sRptID,V.Local.sValues)
		
		F.Global.BI.StopLogging(V.Local.iLogid)
		
		'print report
		F.Global.BI.RunReportPreProcessor(V.Local.iBIRunID,V.Local.iLogID,V.Local.sParams,V.Local.sValues,"",-1,True,"",-1,"",-1,"","",V.Local.iRet)
		
F.Intrinsic.Control.EndSelect

'ending program
F.Intrinsic.Control.CallSub(exit)
	
F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Main_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4588_Order_Acknowledgment.g2u",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(exit)
Function.Intrinsic.Control.EndIf


Program.Sub.Main.End

Program.Sub.Exit.Start
F.Intrinsic.Control.SetErrorHandler("Exit_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)

F.ODBC.Connection!con.Close
F.Intrinsic.Control.End

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Exit_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4588_Order_Acknowledgment.g2u",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End

Function.Intrinsic.Control.EndIf


Program.Sub.Exit.End

Program.Sub.Comments.Start
${$0$}$$}$RLK$}$6/21/2017 3:44:16 PM$}$True
${$3$}$0$}$$}$0$}$-1$}$$}$12:00:00 AM$}$Order Acknowledgment without Prices
Program.Sub.Comments.End